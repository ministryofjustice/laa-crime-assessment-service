plugins {
    id 'java'
    id 'org.springframework.boot' version '3.5.7'
    id 'com.adarshr.test-logger' version '4.0.0'
    id "com.github.ben-manes.versions" version "0.52.0"
    id "com.diffplug.spotless" version "8.0.0"
    id 'jacoco'
}

group = "uk.gov.justice.laa.crime"

java {
    toolchain {
        languageVersion = JavaLanguageVersion.of(21)
    }
}

def versions = [
    sentry                      : "8.31.0",
    resilience4j                : "2.3.0",
    mapstruct                   : "1.6.3",
    crimeCommonsClasses         : "4.28.1",
    crimeCommonsModSchemas      : "1.43.0",
    lombok                      : "1.18.42"
]

repositories {
    mavenCentral()
}

ext {
    set('springModulithVersion', "1.4.7")
}

dependencies {
    // Alignment for all Spring Boot and Spring Modulith dependencies
    implementation platform("org.springframework.boot:spring-boot-dependencies:3.5.10")
    implementation platform("org.springframework.modulith:spring-modulith-bom:1.4.7")
    
    // Spring Modulith
    implementation 'org.springframework.modulith:spring-modulith-starter-core'
    implementation 'org.springframework.modulith:spring-modulith-docs'
    implementation 'org.springframework.modulith:spring-modulith-actuator'
    
    // Spring Boot
    implementation 'org.springframework.boot:spring-boot-starter-actuator'
    implementation 'org.springframework.boot:spring-boot-starter-web'
    implementation 'org.springframework.boot:spring-boot-starter-webflux'
    implementation 'org.springframework.boot:spring-boot-configuration-processor'
    implementation "org.springframework.boot:spring-boot-starter-data-jpa"
    implementation "org.springframework.boot:spring-boot-starter-validation"
    implementation "org.springframework.boot:spring-boot-starter-oauth2-client"
    implementation "org.springframework.boot:spring-boot-starter-oauth2-resource-server"
    implementation "org.springframework.boot:spring-boot-starter-security"

    implementation "org.mapstruct:mapstruct:$versions.mapstruct"

    // Database
    implementation 'org.postgresql:postgresql'
    implementation 'org.liquibase:liquibase-core'

    // Resilience4j
    implementation "io.github.resilience4j:resilience4j-reactor:$versions.resilience4j"
    implementation "io.github.resilience4j:resilience4j-spring-boot3:$versions.resilience4j"

    // Sentry
    implementation platform("io.sentry:sentry-bom:$versions.sentry")
    implementation("io.sentry:sentry-spring-boot-starter-jakarta")
    implementation("io.sentry:sentry-logback")

    // Prometheus
    implementation "io.micrometer:micrometer-registry-prometheus"
    implementation 'io.micrometer:micrometer-tracing'

    // Crime Commons
    implementation "uk.gov.justice.service.laa-crime:crime-commons-classes:$versions.crimeCommonsClasses"
    implementation "uk.gov.justice.service.laa-crime:crime-commons-mod-schemas:$versions.crimeCommonsModSchemas"

    // Testing
    testImplementation 'org.springframework.boot:spring-boot-starter-test'
    testImplementation 'org.springframework.modulith:spring-modulith-starter-test'
    testImplementation 'com.h2database:h2:2.4.240'
    testRuntimeOnly 'org.junit.platform:junit-platform-launcher'

    testImplementation 'org.wiremock.integrations:wiremock-spring-boot:3.10.6'

    // Documentation
    implementation "org.springdoc:springdoc-openapi-starter-webmvc-ui:2.8.15"
    
    compileOnly "org.projectlombok:lombok:$versions.lombok"
    annotationProcessor "org.projectlombok:lombok:$versions.lombok"

    testCompileOnly "org.projectlombok:lombok:$versions.lombok"
    testAnnotationProcessor "org.projectlombok:lombok:$versions.lombok"

    annotationProcessor "org.mapstruct:mapstruct-processor:$versions.mapstruct"
    testAnnotationProcessor "org.mapstruct:mapstruct-processor:$versions.mapstruct"
}

tasks.named('test') {
    useJUnitPlatform()
    finalizedBy jacocoTestReport // report is always generated after tests run
}

jacocoTestReport {
    dependsOn test
    doLast {
        def reportPath = reports.html.outputLocation.get().asFile.absolutePath
        println("JaCoCo report available at: file://$reportPath/index.html")
    }
}

jacocoTestCoverageVerification {
    violationRules {
        rule {
            element = 'CLASS'
            // placeholder exclude for ease of modification.
            excludes = ['uk.gov.justice.laa.crime.assessmentservice.AssessmentServiceApplication',
                        'uk.gov.justice.laa.crime.assessmentservice.ServicesConfiguration',
                        'uk.gov.justice.laa.crime.assessmentservice.WebClientsConfiguration',
                        'uk.gov.justice.laa.crime.assessmentservice.iojappeal.IojAppealController',
                        'uk.gov.justice.laa.crime.assessmentservice.iojappeal.mapper.*',
                        'uk.gov.justice.laa.crime.assessmentservice.passport.PassportController',
                        'uk.gov.justice.laa.crime.assessmentservice.common.filter.*',
                        'uk.gov.justice.laa.crime.assessmentservice.common.exception.*',
                        'uk.gov.justice.laa.crime.assessmentservice.config.*'
            ]
            limit {
                minimum = 0.8
            }
        }
    }
}

spotless {
    java {
        target 'src/*/java/**/*.java'
        targetExclude '**/generated/**', '**/build/**'

        formatAnnotations()
        palantirJavaFormat()
        importOrder('', 'java', 'javax', 'org', 'com')

        endWithNewline()
        trimTrailingWhitespace()
        removeUnusedImports()
        forbidWildcardImports()

        // Custom rule: Disallow JUnit assertions
        custom("Refuse JUnit assertions", { text ->
            if (text =~ /\bassert(?:Equals|True|False|Null|NotNull|Same|NotSame|ArrayEquals)\b/) {
                throw new IllegalStateException(
                        "Use AssertJ (assertThat(...)) instead of JUnit assertions."
                )
            }
            text
        } as Closure<String>)
    }
}

